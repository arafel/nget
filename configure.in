dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.52)
AC_INIT(nget.cc)
AC_CONFIG_HEADER(config.h)    

AC_LANG_CPLUSPLUS

dnl Checks for programs.
AC_PROG_CXX
AC_PROG_INSTALL
dnl AC_PROG_MAKE_SET

MY_CHECK_EXCEPTIONS

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h getopt.h slist.h hash_map.h sstream regex.h term.h inttypes.h)

dnl Checks for libraries.
MY_CHECK_TERMSTUFF

dnl check for liblockfile
usez=yes
AC_ARG_WITH(lockfile,
[ --with-lockfile(=ldir)   Use liblockfile (optionally in ldir).],
	if test "$withval" = "no"; then
		usez=no
	elif test "$withval" != "yes"; then
		usez="$withval"
		CXXFLAGS="$CXXFLAGS -I$withval -L$withval"
		dnl need to set ac_cpp too, so the header check will work
		ac_cpp="$ac_cpp -I$withval"
	fi
)
if test "$usez" != "no"; then
	AC_CHECK_LIB(lockfile, lockfile_create)
	AC_CHECK_HEADERS(lockfile.h)
	if test "$usez" != "yes"; then
		if test "$ac_cv_lib_lockfile_lockfile_create" != "yes" -o "$ac_cv_header_lockfile_h" != "yes" ; then
			echo "lockfile not found in $usez"
			exit 1
		fi
	fi
fi

dnl Check for zlib
usez=yes
AC_ARG_WITH(zlib,
[ --without-zlib           Don't use zlib.  Faster loading/quiting, but uses
                          much more space.
 --with-zlib=zdir         Assume zlib has been compiled in zdir, rather than
                          installed to system dirs.],
	if test "$withval" = "no"; then
		usez=no
	elif test "$withval" != "yes"; then
		usez="$withval"
		CXXFLAGS="$CXXFLAGS -I$withval -L$withval"
	fi
)
if test "$usez" != "no"; then
	AC_CHECK_LIB(z, gzopen)
	if test "$usez" != "yes"; then
		if test "$ac_cv_lib_z_gzopen" != "yes"; then
			echo "zlib not found in $usez"
			exit 1
		fi
	fi
fi

dnl check for pcre (perl-compatible regex)
usez=no
AC_ARG_WITH(pcre,
[ --with-pcre(=pdir)       Use perl-compatible regex library (optionally
                          in pdir).],
	if test "$withval" = "no"; then
		usez=no
	elif test "$withval" != "yes"; then
		usez="$withval"
		CXXFLAGS="$CXXFLAGS -I$withval -L$withval"
		dnl need to set ac_cpp too, so the header check will work
		ac_cpp="$ac_cpp -I$withval"
	else
		usez=yes
	fi
)
if test "$usez" != "no"; then
	AC_CHECK_LIB(pcre,pcre_compile)
	AC_CHECK_LIB(pcreposix, regexec)
	AC_CHECK_HEADERS(pcreposix.h)
	if test "$usez" != "yes"; then
		if test "$ac_cv_lib_pcreposix_regexec" != "yes" -o "$ac_cv_lib_pcre_pcre_compile" != "yes" -o "$ac_cv_header_pcreposix_h" != "yes" ; then
			echo "pcre not found in $usez"
			exit 1
		fi
	fi
fi

dnl check for libpopt
usez=yes
AC_ARG_WITH(popt,
[ --with-popt(=pdir)       Use libpopt (getopt replacement) (optionally in pdir)],
	if test "$withval" = "no"; then
		usez=no
	elif test "$withval" != "yes"; then
		usez="$withval"
		CXXFLAGS="$CXXFLAGS -I$withval -L$withval"
		dnl need to set ac_cpp too, so the header check will work
		ac_cpp="$ac_cpp -I$withval"
	fi
)
if test "$usez" != "no"; then
	AC_CHECK_LIB(popt, poptGetContext)
	AC_CHECK_HEADERS(popt.h)
	if test "$usez" != "yes"; then
		if test "$ac_cv_lib_popt_poptGetContext" != "yes" -o "$ac_cv_header_popt_h" != "yes" ; then
			echo "popt not found in $usez"
			exit 1
		fi
	fi
fi
if test "$ac_cv_header_popt_h" = "yes" ; then
	MY_CHECK_POPT_CONST
fi

dnl check for uulib
if test -d uulib; then 
	UUDIR=uulib
	uumanual=yes
else
	uumanual=no
fi
AC_ARG_WITH(uulib,
[ --with-uulib=UULIBDIR    specify dir containing uulib headers and libuu.a, if
                          not specified, configure will check in system dirs.],
	if test "$withval" != "no"; then
		UUDIR=$withval
	fi
	uumanual=yes
)

if test "$uumanual" = "yes"; then
	UUINC=-I$UUDIR
	UULIB=$UUDIR/libuu.a
	AC_DEFINE(HAVE_LIBUU)
else
	NOUU=#
	AC_CHECK_LIB(uulib, UUInitialize)
	if test "$ac_cv_lib_uulib_UUInitialize" != "yes"; then
		AC_CHECK_LIB(uu, UUInitialize)
	fi
	AC_CHECK_HEADERS(uudeview.h)
fi

AC_SUBST(NOUU)
AC_SUBST(UUINC)
AC_SUBST(UUDIR)
AC_SUBST(UULIB)


AC_ARG_ENABLE(filecompare,
[ --disable-filecompare    Do not test to see if a duplicate downloaded file is
                          the same as the original, keep all duplicates.],
	if test "$enableval" != "no"; then
		AC_DEFINE(USE_FILECOMPARE)
	fi
,
		AC_DEFINE(USE_FILECOMPARE)
)
AC_ARG_ENABLE(checksum,
[ --enable-checksum=method Set which method to use for comparing headers and
                          for short-tempnames.  Either crc32 or adler32.
                          default is crc32, as it seems to get less repeated
                          values, however adler32 is said to be faster.  It
                          probably doesn't make much difference.
 --disable-checksum       Disable usage of checksums for comparing headers as
                          well as for short-tempnames, uses STL hash function
                          instead.],
	if test "$enableval" != "no"; then
		if test "$enableval" != "yes"; then
			AC_DEFINE_UNQUOTED(CHECKSUM,$enableval)
		else
			AC_DEFINE(CHECKSUM,crc32)
		fi
	fi
,
	AC_DEFINE(CHECKSUM,crc32)
)

AC_ARG_ENABLE(debug,
 [ --enable-debug           Enable debug code, asserts, etc.],
 	if test "$enableval" != "no"; then
 		nget_debug=yes
 	fi
)
if test "$nget_debug" = "yes"; then
	MY_DISABLE_OPT
else
	AC_DEFINE(NDEBUG)
fi
dnl AC_ARG_ENABLE(debug_cache,
dnl [ --enable-debug_cache     Enable consistancy checks when loading and saving header cache],
dnl 	if test "$enableval" != "no"; then
dnl 		AC_DEFINE(DEBUG_CACHE)
dnl 	fi
dnl )


dnl checks for socklen_t
SOCK_CHECK_TYPE(socklen_t, int)

AC_CHECK_TYPES([long long, int_fast64_t, uint_fast64_t])

dnl checks for ulong
AC_CHECK_TYPE(ulong, unsigned long)
dnl checks for uchar
AC_CHECK_TYPE(uchar, unsigned char)

dnl see if timezone is an int
MY_DECL_TIMEZONE
MY_DECL__TIMEZONE

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
dnl AC_FUNC_VPRINTF
AC_CHECK_FUNCS(mkdir mktime regcomp select socket strerror atoul asprintf vasprintf vdprintf getservbyname_r gethostbyname_r localtime_r inet_aton getaddrinfo getnameinfo getopt_long flock)

MY_CHECK_OPTIMIZATION

AC_OUTPUT(Makefile,[echo timestamp > stamp-h])

